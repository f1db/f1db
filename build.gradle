plugins {
    id "java"
    id "distribution"
    id "org.jreleaser" version "1.1.0"
}

apply plugin: "net.f1db"

group = "net.f1db"

repositories {
    mavenCentral()
}

distributions {
    "csv" {
        distributionBaseName = "f1db-csv"
        contents {
            from "${buildDir}/data/csv"
            include "*.csv"
        }
    }
    "json" {
        distributionBaseName = "f1db-json"
        contents {
            from "${buildDir}/data/json/f1db.json"
            from "${projectDir}/src/schema/v${property("jsonSchemaVersion")}/f1db.json-schema.json"
        }
    }
    "json-splitted" {
        distributionBaseName = "f1db-json-splitted"
        contents {
            from("${buildDir}/data/json") {
                include "*.json"
                exclude "f1db.json"
            }
            from("${projectDir}/src/schema/v${property("jsonSchemaVersion")}/splitted") {
                include "*.json"
            }
        }
    }
    "smile" {
        distributionBaseName = "f1db-smile"
        contents {
            from "${buildDir}/data/smile/f1db.sml"
            from "${projectDir}/src/schema/v${property("jsonSchemaVersion")}/f1db.json-schema.json"
        }
    }
    "smile-splitted" {
        distributionBaseName = "f1db-smile-splitted"
        contents {
            from("${buildDir}/data/smile") {
                include "*.sml"
                exclude "f1db.sml"
            }
            from("${projectDir}/src/schema/v${property("jsonSchemaVersion")}/splitted") {
                include "*.json"
            }
        }
    }
    "sqlite" {
        distributionBaseName = "f1db-sqlite"
        contents {
            from "${buildDir}/data/sqlite/f1db.db"
        }
    }
}

f1db {
    jsonSchemaVersion = property("jsonSchemaVersion")
    currentSeason = property("currentSeason").toInteger()
    currentSeasonFinished = property("currentSeasonFinished").toBoolean()
    wdcDecided = property("wdcDecided").toBoolean()
    wccDecided = property("wccDecided").toBoolean()
    indentOutput = false
}

jreleaser {
    project {
        name = "f1db"
        versionPattern = "CALVER:YYYY.MINOR.MICRO[.MODIFIER]"
        description = "Open Source Formula 1 Database"
        website = "https://www.f1db.net"
        license = "CC-BY-4.0"
        licenseUrl = "https://spdx.org/licenses/CC-BY-4.0.html"
        copyright = "F1DB"
    }
    files {
        artifact {
            path = "${buildDir}/distributions/f1db-csv-{{projectVersion}}.zip"
        }
        artifact {
            path = "${buildDir}/distributions/f1db-json-{{projectVersion}}.zip"
        }
        artifact {
            path = "${buildDir}/distributions/f1db-json-splitted-{{projectVersion}}.zip"
        }
        artifact {
            path = "${buildDir}/distributions/f1db-smile-{{projectVersion}}.zip"
        }
        artifact {
            path = "${buildDir}/distributions/f1db-smile-splitted-{{projectVersion}}.zip"
        }
        artifact {
            path = "${buildDir}/distributions/f1db-sqlite-{{projectVersion}}.zip"
        }
    }
    release {
        github {
            enabled = true
            owner = "f1db"
            name = "f1db"
            username = "marceloverdijk"
            commitAuthor {
                name = "F1DB"
                email = "info@f1db.net"
            }
            skipTag = true
        }
    }
}

tasks.withType(Tar) {
    enabled = false
}
